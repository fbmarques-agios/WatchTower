// INSTRUCTIONS
// - Add the following dependencies to your Arduino libraries:
//     - Adafruit NeoPixel ~1.15.2
//     - ESPUI ~2.2.4
//     - ESP32Async / ESP Async WebServer ~3.9.0
//     - ESP32Async / Async TCP ~3.4.9
//     - WiFiManager ~2.0.17
//     - ArduinoMDNS ~1.0.0
//     - M5Unified (apenas no M5StickC Plus -- mostra o status no LCD)
// - set the PIN_ANTENNA to desired output pin
// - set the timezone as desired
// - build and run the code on your device
// - connect your phone to "WatchTower" to set the wifi config for the device
// - connect to http://watchtower.local to view current status

// Designed for the following, but should be easily
// transferable to other components:
// - Adafruit Qt Py ESP32 Pico: https://www.adafruit.com/product/5395
// - Adafruit DRV8833 breakout: https://www.adafruit.com/product/3297
// Also tested on
// - Adafruit ESP32 Feather v2
// - Arduino Nano ESP32 (via wokwi)

#include <WiFiManager.h>
#include <Adafruit_NeoPixel.h>
#include <SPI.h>
#include <ESPUI.h>
#include <WiFiUdp.h>
#include <ArduinoMDNS.h>
#include <time.h>
#include <esp_sntp.h>
#include "customJS.h"
#ifndef UNIT_TEST
#include <M5Unified.h>  // LCD + AXP192 do M5StickC Plus
#include "driver/gpio.h" // gpio_set_drive_capability (limita a corrente da antena)
#else
// Native test build (env:native, -D UNIT_TEST): stub the embedded-only
// dependencies so test_bootstrap.cpp can include WatchTower.ino.
typedef int gpio_num_t;
typedef int gpio_drive_cap_t;
static inline int gpio_set_drive_capability(gpio_num_t, gpio_drive_cap_t) { return 0; }
#ifndef constrain
#define constrain(x, a, b) ((x) < (a) ? (a) : ((x) > (b) ? (b) : (x)))
#endif
#endif

// Flip to false to disable the built-in web ui.
// You might want to do this to avoid leaving unnecessary open ports on your network.
const bool ENABLE_WEB_UI = true;

// Set this to the pin your antenna is connected on.
// M5StickC Plus: GPIO13 e usado pelo LCD interno, entao usamos o GPIO26,
// que e um pino livre e exposto no header superior da placa.
const int PIN_ANTENNA = 26;

// Forca de saida (drive strength) do pino da antena -- 0 a 3:
//   0 = mais fraco (~5 mA)   -- mais seguro, sinal fraco
//   1 = fraco      (~10 mA)
//   2 = padrao     (~20 mA)  -- forca normal de um GPIO do ESP32
//   3 = maximo     (~40 mA)  -- USE SOMENTE com resistor ~100 ohm em serie
// Sem resistor em serie, use no maximo o nivel 2. Mais voltas na bobina
// ajudam mais (e com mais seguranca) do que aumentar este nivel.
const int ANTENNA_DRIVE_LEVEL = 3;

// Set to your timezone.
// This is needed for computing DST if applicable
// https://gist.github.com/alwynallan/24d96091655391107939
// Brasil (horario de Brasilia, UTC-3, sem horario de verao desde 2019).
// Para outros fusos: Acre "ACT5", Amazonas/Mato Grosso "AMT4".
const char *timezone = "BRT3"; // America/Sao_Paulo


enum WWVB_T {
  ZERO = 0,
  ONE = 1,
  MARK = 2,
};

const int KHZ_60 = 60000;
const char* const ntpServer = "pool.ntp.org";

// Configure the optional onboard neopixel
#ifdef PIN_NEOPIXEL
Adafruit_NeoPixel* const pixel = new Adafruit_NeoPixel(1, PIN_NEOPIXEL, NEO_GRB + NEO_KHZ800);
#else
Adafruit_NeoPixel* const pixel = NULL;
#endif

const uint8_t LED_BRIGHTNESS = 10; // very dim, 0-255
const uint32_t COLOR_READY = pixel ? pixel->Color(0, 60, 0) : 0; // green https://share.google/4WKm4XDkH9tfm3ESC
const uint32_t COLOR_LOADING = pixel ? pixel->Color(60, 32, 0) : 0; // orange https://share.google/7tT5GPxskZi8t8qmx
const uint32_t COLOR_ERROR = pixel ? pixel->Color(150, 0, 0) : 0; // red https://share.google/nx2jWYSoGtl0opkzL
const uint32_t COLOR_TRANSMIT = pixel ? pixel->Color(32, 0, 0) : 0; // dim red https://share.google/wYFYM3t1kDeOJfr1U

WiFiManager wifiManager;
WiFiUDP udp;
MDNS mdns(udp);
bool logicValue = 0; // TODO rename
struct timeval lastSync;
WWVB_T broadcast[60];

// ESPUI Interface IDs
uint16_t ui_time;
uint16_t ui_date;
uint16_t ui_timezone;
uint16_t ui_broadcast;
uint16_t ui_uptime;
uint16_t ui_last_sync;

// ===== LCD do M5StickC Plus =====
// O projeto original nao usa display. Aqui inicializamos o LCD do
// M5StickC Plus via M5Unified (que tambem liga o backlight pelo AXP192)
// e mostramos o status na telinha (orientacao horizontal, 240x135).
// Excluido da build de testes nativos (UNIT_TEST) -- ver stubs no final do bloco.

// Cores RGB565 (definidas fora do #ifndef porque o arduino-builder
// gera prototipos varrendo ambos os ramos e avisaria de redefinicao
// se elas fossem duplicadas em cada branch).
#define C_BLACK  0x0000
#define C_WHITE  0xFFFF
#define C_CYAN   0x07FF
#define C_GREEN  0x07E0
#define C_RED    0xF800
#define C_GREY   0x7BEF
#define C_YELLOW 0xFFE0

#ifndef UNIT_TEST

// Inicializa o display e desenha o cabecalho fixo.
void displayInit() {
  auto cfg = M5.config();
  M5.begin(cfg);
  M5.Display.setRotation(1);            // horizontal: 240 x 135
  M5.Display.fillScreen(C_BLACK);
  M5.Display.setTextSize(2);
  M5.Display.setTextColor(C_CYAN, C_BLACK);
  M5.Display.setCursor(6, 6);
  M5.Display.print("WatchTower");
  M5.Display.setTextColor(C_RED, C_BLACK);
  M5.Display.setCursor(204, 6);
  M5.Display.print("TX");
  M5.Display.drawFastHLine(0, 28, 240, C_GREY);
}

// Mostra uma mensagem de status em duas linhas (fases de boot / erros).
void displayStatus(const char* line1, const char* line2, uint16_t color) {
  M5.Display.fillRect(0, 30, 240, 105, C_BLACK);
  M5.Display.setTextSize(2);
  M5.Display.setTextColor(color, C_BLACK);
  M5.Display.setCursor(6, 48);
  M5.Display.print(line1);
  M5.Display.setCursor(6, 78);
  M5.Display.print(line2);
}

// Mostra o estado de operacao: data, hora grande e IP (chamado 1x/seg).
void displayRunning(const struct tm* lt) {
  static bool first = true;
  if (first) {
    M5.Display.fillRect(0, 30, 240, 105, C_BLACK);
    M5.Display.setTextSize(2);
    M5.Display.setTextColor(C_CYAN, C_BLACK);
    M5.Display.setCursor(6, 110);
    M5.Display.print(WiFi.localIP().toString().c_str());
    first = false;
  }
  char b[32];
  strftime(b, sizeof(b), "%a %d/%m/%Y", lt);
  M5.Display.setTextSize(2);
  M5.Display.setTextColor(C_WHITE, C_BLACK);
  M5.Display.setCursor(6, 38);
  M5.Display.print(b);

  strftime(b, sizeof(b), "%H:%M:%S", lt);
  M5.Display.setTextSize(4);
  M5.Display.setTextColor(C_GREEN, C_BLACK);
  M5.Display.setCursor(6, 64);
  M5.Display.print(b);
}

#else  // UNIT_TEST: stubs no-op para a build de testes nativos
void displayInit() {}
void displayStatus(const char*, const char*, uint16_t) {}
void displayRunning(const struct tm*) {}
#endif

// A callback that tracks when we last sync'ed the
// time with the ntp server
void time_sync_notification_cb(struct timeval *tv) {
  lastSync = *tv;
}

// A callback that is called when the device
// starts up an access point for wifi configuration.
// This is called when the device cannot connect to wifi.
void accesspointCallback(WiFiManager*) {
  Serial.println("Connect to SSID: WatchTower with another device to set wifi configuration.");
  displayStatus("Configure o WiFi", "rede: WatchTower", C_YELLOW);
}

// Convert a logical bit into a PWM pulse width.
// Returns 50% duty cycle (128) for high, 0% for low
static inline short dutyCycle(bool logicValue) {
  return logicValue ? (256*0.5) : 0; // 128 == 50% duty cycle
}

static inline int is_leap_year(int year) {
    return (year % 4 == 0) && (year % 100 != 0 || year % 400 == 0);
}

void clearBroadcastValues() {
    for(int i=0; i<sizeof(broadcast)/sizeof(broadcast[0]); ++i) {
        broadcast[i] = (WWVB_T)-1; // -1 isn't legal but that's okay, we just need an invalid value
    }
}

void setup() {
  Serial.begin(115200);
  delay(1000);

  // Inicializa o LCD do M5StickC Plus e mostra o status do boot
  displayInit();
  displayStatus("Iniciando...", "", C_WHITE);

  pinMode(PIN_ANTENNA, OUTPUT);
  if( pixel ) {
    pixel->begin();
    pixel->setBrightness(LED_BRIGHTNESS); // very dim
    pixel->setPixelColor(0, COLOR_LOADING );
    pixel->show();
  }

  // E (14621) rmt: rmt_new_tx_channel(269): not able to power down in light sleep
  digitalWrite(PIN_ANTENNA, 0);

  // https://github.com/tzapu/WiFiManager/issues/1426
  WiFi.setScanMethod(WIFI_ALL_CHANNEL_SCAN);

  // Connect to WiFi using // https://github.com/tzapu/WiFiManager 
  // If no wifi, start up an SSID called "WatchTower" so
  // the user can configure wifi using their phone.
  wifiManager.setAPCallback(accesspointCallback);
  displayStatus("Conectando ao", "WiFi...", C_WHITE);
  wifiManager.autoConnect("WatchTower");
#ifndef UNIT_TEST
  displayStatus("WiFi conectado!", WiFi.localIP().toString().c_str(), C_GREEN);
#endif

  clearBroadcastValues();

  // --- ESPUI SETUP ---
  ESPUI.setVerbosity(Verbosity::Quiet);
  
  // Create Labels
  ui_broadcast = ESPUI.label("Broadcast Waveform", ControlColor::Sunflower, "");
  ui_time = ESPUI.label("Current Time", ControlColor::Turquoise, "Loading...");
  ui_date = ESPUI.label("Date", ControlColor::Emerald, "Loading...");
  ui_timezone = ESPUI.label("Timezone", ControlColor::Peterriver, timezone);
  ui_uptime = ESPUI.label("System Uptime", ControlColor::Carrot, "0s");
  ui_last_sync = ESPUI.label("Last NTP Sync", ControlColor::Alizarin, "Pending...");

  ESPUI.setPanelWide(ui_broadcast, true);
  ESPUI.setElementStyle(ui_broadcast, "font-family: monospace");
  ESPUI.setCustomJS(customJS);

  // You may disable the internal webserver by commenting out this line
  if( ENABLE_WEB_UI ) {
    mdns.begin(WiFi.localIP(), "watchtower");
    Serial.println("Connect to http://watchtower.local for the console");
    ESPUI.begin("WatchTower");
  }
  
  // --- TIME SYNC ---

  // Connect to network time server
  // By default, it will resync every few hours
  sntp_set_time_sync_notification_cb(time_sync_notification_cb);
  configTzTime(timezone, ntpServer);
  
  struct tm timeinfo;
  if(!getLocalTime(&timeinfo)){
    Serial.println("Failed to obtain time");
    displayStatus("Falha no NTP", "reiniciando...", C_RED);
    if( pixel ) {
        pixel->setPixelColor(0, COLOR_ERROR );
        pixel->show();
    }
    delay(3000);
    ESP.restart();
  }
  Serial.println("Got the time from NTP");
  displayStatus("Sincronizado!", "Transmitindo...", C_GREEN);

  // Start the 60khz carrier signal using 8-bit (0-255) resolution
  ledcAttach(PIN_ANTENNA, KHZ_60, 8);

  // Ajusta a forca de saida do pino da antena (ver ANTENNA_DRIVE_LEVEL).
  gpio_set_drive_capability((gpio_num_t)PIN_ANTENNA,
                            (gpio_drive_cap_t)constrain(ANTENNA_DRIVE_LEVEL, 0, 3));

  // green means go
  if( pixel ) {
    pixel->setPixelColor(0, COLOR_READY );
    pixel->show();
    delay(3000);
    pixel->clear();  
    pixel->show();
  }
}

void loop() {
  mdns.run();

  struct timeval now; // current time in seconds / millis
  struct tm buf_now_utc; // current time in UTC
  struct tm buf_now_local; // current time in localtime
  struct tm buf_today_start, buf_tomorrow_start; // start of today and tomrrow in localtime
  static int prev_second_display = -1; // for tracking UI updates

  gettimeofday(&now,NULL);
  localtime_r(&now.tv_sec, &buf_now_local);
  gmtime_r(&now.tv_sec, &buf_now_utc); 

  // compute start of today for dst
  struct timeval today_start = now;
  today_start.tv_usec = 0;
  today_start.tv_sec = (today_start.tv_sec / 86400) * 86400; // This is not exact but close enough
  localtime_r(&today_start.tv_sec, &buf_today_start);

  // compute start of tomorrow for dst
  struct timeval tomorrow_start = now;
  tomorrow_start.tv_usec = 0;
  tomorrow_start.tv_sec = ((tomorrow_start.tv_sec / 86400) + 1) * 86400; // again, close enough
  localtime_r(&tomorrow_start.tv_sec, &buf_tomorrow_start);

  const bool prevLogicValue = logicValue;

  logicValue = wwvbLogicSignal(
    buf_now_utc.tm_hour,
    buf_now_utc.tm_min,
    buf_now_utc.tm_sec, 
    now.tv_usec/1000,
    buf_now_utc.tm_yday+1,
    buf_now_utc.tm_year+1900,
    buf_today_start.tm_isdst,
    buf_tomorrow_start.tm_isdst
    );

  // --- UI UPDATE LOGIC ---
  if( logicValue != prevLogicValue ) {
    ledcWrite(PIN_ANTENNA, dutyCycle(logicValue));  // Update the duty cycle of the PWM

    // light up the pixel if desired
    if( pixel ) {
      if( logicValue == 1 ) {
        pixel->setPixelColor(0, COLOR_TRANSMIT ); // don't call show yet, the color may change
      } else {
        pixel->clear();
      }
    }

    // do any logging after we set the bit to not slow anything down,
    // serial port I/O is slow!
    char timeStringBuff[100]; // Buffer to hold the formatted time string
    char timeStringBuff2[100];
    char timeStringBuff3[20];
    strftime(timeStringBuff, sizeof(timeStringBuff), "%A, %B %d %Y %H:%M:%S", &buf_now_local); // time
    strftime(timeStringBuff3, sizeof(timeStringBuff3), "%z %Z", &buf_now_local); // timezone
    sprintf(timeStringBuff2,"%s.%03d%s", timeStringBuff, now.tv_usec/1000, timeStringBuff3 ); // time+millis+tz

    char lastSyncStringBuff[100]; // Buffer to hold the formatted time string
    struct tm buf_lastSync;
    localtime_r(&lastSync.tv_sec, &buf_lastSync);
    strftime(lastSyncStringBuff, sizeof(lastSyncStringBuff), "%b %d %H:%M", &buf_lastSync);
    Serial.printf("%s [last sync %s]: %s\n",timeStringBuff2, lastSyncStringBuff, logicValue ? "1" : "0");

    static int prevSecond = -1;
    if( prevSecond != buf_now_utc.tm_sec ) {
        prevSecond = buf_now_utc.tm_sec;

        // --- UPDATE THE WEB UI ---

        // Time
        char buf[62];
        strftime(buf, sizeof(buf), "%H:%M:%S%z %Z", &buf_now_local);
        ESPUI.print(ui_time, buf);

        // Date
        strftime(buf, sizeof(buf), "%A, %B %d %Y", &buf_now_local);
        ESPUI.print(ui_date, buf);

        // Broadcast window
        for( int i=0; i<60; ++i ) { // TODO leap seconds
        switch(broadcast[i]) {
            case WWVB_T::MARK:
                buf[i] = 'M';
                break;
            case WWVB_T::ZERO:
                buf[i] = '0';
                break;
            case WWVB_T::ONE:
                buf[i] = '1';
                break;
            default:
                buf[i] = ' ';
                break;
        }
        }
        ESPUI.print(ui_broadcast, buf);


        // Uptime
        long uptime = millis() / 1000;
        int up_d = uptime / 86400;
        int up_h = (uptime % 86400) / 3600;
        int up_m = (uptime % 3600) / 60;
        int up_s = uptime % 60;
        snprintf(buf, sizeof(buf), "%03dd %02dh %02dm %02ds", up_d, up_h, up_m, up_s);
        ESPUI.print(ui_uptime, buf);

        // Last Sync
        strftime(buf, sizeof(buf), "%b %d %H:%M", &buf_lastSync);
        ESPUI.print(ui_last_sync, buf);

        // Atualiza o LCD do M5StickC Plus
        displayRunning(&buf_now_local);
    }

    // Check for stale sync (24 hours)
    if( now.tv_sec - lastSync.tv_sec > 60 * 60 * 24 ) {
      Serial.println("Last sync more than 24 hours ago, rebooting.");
      displayStatus("Sync ha +24h", "reiniciando...", C_RED);
      if( pixel ) {
        pixel->setPixelColor(0, COLOR_ERROR );
        delay(3000);
      }
      ESP.restart();
    }

    if( pixel ) {
        pixel->show();
    }
  }  
}


// Returns a logical high or low to indicate whether the
// PWM signal should be high or low based on the current time
// https://www.nist.gov/pml/time-and-frequency-division/time-distribution/radio-station-wwvb/wwvb-time-code-format
bool wwvbLogicSignal(
    int hour,                // 0 - 23
    int minute,              // 0 - 59
    int second,              // 0 - 59 (leap 60)
    int millis,
    int yday,                // days since January 1 eg. Jan 1 is 0
    int year,                // year since 0, eg. 2025
    int today_start_isdst,   // was this morning DST?
    int tomorrow_start_isdst // is tomorrow morning DST?
) {
    int leap = is_leap_year(year);
    
    WWVB_T bit;
    switch (second) {
        case 0: // mark
            bit = WWVB_T::MARK;
            break;
        case 1: // minute 40
            bit = (WWVB_T)(((minute / 10) >> 2) & 1);
            break;
        case 2: // minute 20
            bit = (WWVB_T)(((minute / 10) >> 1) & 1);
            break;
        case 3: // minute 10
            bit = (WWVB_T)(((minute / 10) >> 0) & 1);
            break;
        case 4: // blank
            bit = WWVB_T::ZERO;
            break;
        case 5: // minute 8
            bit = (WWVB_T)(((minute % 10) >> 3) & 1);
            break;
        case 6: // minute 4
            bit = (WWVB_T)(((minute % 10) >> 2) & 1);
            break;
        case 7: // minute 2
            bit = (WWVB_T)(((minute % 10) >> 1) & 1);
            break;
        case 8: // minute 1
            bit = (WWVB_T)(((minute % 10) >> 0) & 1);
            break;
        case 9: // mark
            bit = WWVB_T::MARK;
            break;
        case 10: // blank
            bit = WWVB_T::ZERO;
            break;
        case 11: // blank
            bit = WWVB_T::ZERO;
            break;
        case 12: // hour 20
            bit = (WWVB_T)(((hour / 10) >> 1) & 1);
            break;
        case 13: // hour 10
            bit = (WWVB_T)(((hour / 10) >> 0) & 1);
            break;
        case 14: // blank
            bit = WWVB_T::ZERO;
            break;
        case 15: // hour 8
            bit = (WWVB_T)(((hour % 10) >> 3) & 1);
            break;
        case 16: // hour 4
            bit = (WWVB_T)(((hour % 10) >> 2) & 1);
            break;
        case 17: // hour 2
            bit = (WWVB_T)(((hour % 10) >> 1) & 1);
            break;
        case 18: // hour 1
            bit = (WWVB_T)(((hour % 10) >> 0) & 1);
            break;
        case 19: // mark
            bit = WWVB_T::MARK;
            break;
        case 20: // blank
            bit = WWVB_T::ZERO;
            break;
        case 21: // blank
            bit = WWVB_T::ZERO;
            break;
        case 22: // yday of year 200
            bit = (WWVB_T)(((yday / 100) >> 1) & 1);
            break;
        case 23: // yday of year 100
            bit = (WWVB_T)(((yday / 100) >> 0) & 1);
            break;
        case 24: // blank
            bit = WWVB_T::ZERO;
            break;
        case 25: // yday of year 80
            bit = (WWVB_T)((((yday / 10) % 10) >> 3) & 1);
            break;
        case 26: // yday of year 40
            bit = (WWVB_T)((((yday / 10) % 10) >> 2) & 1);
            break;
        case 27: // yday of year 20
            bit = (WWVB_T)((((yday / 10) % 10) >> 1) & 1);
            break;
        case 28: // yday of year 10
            bit = (WWVB_T)((((yday / 10) % 10) >> 0) & 1);
            break;
        case 29: // mark
            bit = WWVB_T::MARK;
            break;
        case 30: // yday of year 8
            bit = (WWVB_T)(((yday % 10) >> 3) & 1);
            break;
        case 31: // yday of year 4
            bit = (WWVB_T)(((yday % 10) >> 2) & 1);
            break;
        case 32: // yday of year 2
            bit = (WWVB_T)(((yday % 10) >> 1) & 1);
            break;
        case 33: // yday of year 1
            bit = (WWVB_T)(((yday % 10) >> 0) & 1);
            break;
        case 34: // blank
            bit = WWVB_T::ZERO;
            break;
        case 35: // blank
            bit = WWVB_T::ZERO;
            break;
        case 36: // UTI sign +
            bit = WWVB_T::ONE;
            break;
        case 37: // UTI sign -
            bit = WWVB_T::ZERO;
            break;
        case 38: // UTI sign +
            bit = WWVB_T::ONE;
            break;
        case 39: // mark
            bit = WWVB_T::MARK;
            break;
        case 40: // UTI correction 0.8
            bit = WWVB_T::ZERO;
            break;
        case 41: // UTI correction 0.4
            bit = WWVB_T::ZERO;
            break;
        case 42: // UTI correction 0.2
            bit = WWVB_T::ZERO;
            break;
        case 43: // UTI correction 0.1
            bit = WWVB_T::ZERO;
            break;
        case 44: // blank
            bit = WWVB_T::ZERO;
            break;
        case 45: // year 80
            bit = (WWVB_T)((((year / 10) % 10) >> 3) & 1);
            break;
        case 46: // year 40
            bit = (WWVB_T)((((year / 10) % 10) >> 2) & 1);
            break;
        case 47: // year 20
            bit = (WWVB_T)((((year / 10) % 10) >> 1) & 1);
            break;
        case 48: // year 10
            bit = (WWVB_T)((((year / 10) % 10) >> 0) & 1);
            break;
        case 49: // mark
            bit = WWVB_T::MARK;
            break;
        case 50: // year 8
            bit = (WWVB_T)(((year % 10) >> 3) & 1);
            break;
        case 51: // year 4
            bit = (WWVB_T)(((year % 10) >> 2) & 1);
            break;
        case 52: // year 2
            bit = (WWVB_T)(((year % 10) >> 1) & 1);
            break;
        case 53: // year 1
            bit = (WWVB_T)(((year % 10) >> 0) & 1);
            break;
        case 54: // blank
            bit = WWVB_T::ZERO;
            break;
        case 55: // leap year
            bit = leap ? WWVB_T::ONE : WWVB_T::ZERO;
            break;
        case 56: // leap second
            bit = WWVB_T::ZERO;
            break;
        case 57: // dst bit 1
            bit = today_start_isdst ? WWVB_T::ONE : WWVB_T::ZERO;
            break;
        case 58: // dst bit 2
            bit = tomorrow_start_isdst ? WWVB_T::ONE : WWVB_T::ZERO;
            break;
        case 59: // mark
            bit = WWVB_T::MARK;
            break;
    }

    if(second == 0) {
        clearBroadcastValues();
    }
    broadcast[second] = bit;

    // Convert a wwvb zero, one, or mark to the appropriate pulse width
    // zero: low 200ms, high 800ms
    // one: low 500ms, high 500ms
    // mark low 800ms, high 200ms
    if (bit == WWVB_T::ZERO) {
      return millis >= 200;
    } else if (bit == WWVB_T::ONE) {
      return millis >= 500;
    } else {
      return millis >= 800;
    }
}
